/*
 * Copyright 2014 Google Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'

repositories {
    jcenter()
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "com.google.samples.apps.iosched"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 223
        versionName "2.2.3"
    }

    signingConfigs {
        debug {
            storeFile file(iosched14_android_debugkey_storefile)
            storePassword iosched14_android_debugkey_storePassword
            keyAlias iosched14_android_debugkey_keyAlias
            keyPassword iosched14_android_debugkey_keyPassword
        }
        release {
            storeFile file(iosched14_android_releasekey_storefile)
            storePassword iosched14_android_releasekey_storePassword
            keyAlias iosched14_android_releasekey_keyAlias
            keyPassword iosched14_android_releasekey_keyPassword
        }
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-project.txt')
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }

    aaptOptions {
        noCompress 'apk'
    }
}

dependencies {
    wearApp project(':Wearable')
    compile 'com.android.support:support-v4:21.0.0'
    compile 'com.android.support:support-v13:21.0.0'
    compile 'com.android.support:appcompat-v7:21.0.0'
    compile 'com.android.support:cardview-v7:21.0.0'
    compile 'com.google.android.gms:play-services:6.1.71'

    compile 'com.google.code.gson:gson:2.3'

    compile('com.google.api-client:google-api-client:1.19.0') {
        exclude group: 'xpp3', module: 'shared'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        exclude group: 'junit', module: 'junit'
        exclude group: 'com.google.android', module: 'android'
    }
    compile 'com.google.api-client:google-api-client-android:1.19.0'
    compile 'com.google.apis:google-api-services-plus:v1-rev193-1.19.0'
    compile 'com.github.japgolly.android:svg-android:2.0.6'
    compile fileTree(dir: 'libs', include: '*.jar')
    compile files('../third_party/glide/library/libs/glide-3.2.0a.jar')
    compile files('../third_party/basic-http-client/libs/basic-http-client-android-0.88.jar')

    // Google Maps Android API utility library
    compile('com.google.maps.android:android-maps-utils:0.3.3') {
        exclude group: "com.google.android.gms"
    }

    /* The two dependencies below are only required while we are using http
       Drive appdata. When we start using AppData through GMS,
       they should be removed. */
    compile 'com.google.http-client:google-http-client-gson:1.19.0'
    compile 'com.google.apis:google-api-services-drive:v2-rev151-1.19.0'
}

// TODO: Add tests and delete this
project.gradle.taskGraph.whenReady {
    connectedAndroidTest {
        ignoreFailures = true
    }
}