/*
 * Copyright 2014 Google Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'

repositories {
    jcenter()
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion rootProject.ext.androidBuildToolsVersion
    defaultConfig {
        applicationId rootProject.ext.androidDefaultApplicationId
        minSdkVersion 20
        targetSdkVersion rootProject.ext.androidDefaultTargetSdkVersion
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        debug {
            storeFile file(rootProject.ext.androidDebugStoreFile)
            storePassword rootProject.ext.androidDebugStorePassword
            keyAlias rootProject.ext.androidDebugKeyAlias
            keyPassword rootProject.ext.androidDebugKeyPassword
        }
        release {
            storeFile file(rootProject.ext.androidReleaseStoreFile)
            storePassword rootProject.ext.androidReleaseStorePassword
            keyAlias rootProject.ext.androidReleaseKeyAlias
            keyPassword rootProject.ext.androidReleaseKeyPassword
        }
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile ('com.android.support:support-annotations:21.0.2') {
        force = true
    }
    compile ('com.android.support:support-v4:21.0.2') {
        force = true
    }
    compile('com.android.support:support-v13:21.0.0') {
        // Conflicting module version com.android.support:support-v4:21.0.0 on v.21.0.0.
        exclude (group: 'com.android.support', module: 'support-v4')
    }
    compile(group: 'com.google.android.support', name: 'wearable', version: '1.0.0') {
        // Conflicting module version com.android.support:support-v4:20.0.0 on v.1.0.0.
        exclude (group: 'com.android.support', module: 'support-v4')
        // Conflicting module version com.android.support:support-v13:20.0.0 on v.1.0.0.
        exclude (group: 'com.android.support', module: 'support-v13')
    }
    compile(group: 'com.google.android.gms', name: 'play-services-wearable', version: '6.1.71') {
        // Conflicting module version com.android.support:support-v4:20.0.0 on v.6.1.71.
        exclude (group: 'com.android.support', module: 'support-v4')
    }
}

// TODO: Add tests and delete this. Valorate to add extra time for tests.
// See https://code.google.com/p/android/issues/detail?id=69735
// See https://code.google.com/p/android/issues/detail?id=76249
project.gradle.taskGraph.whenReady {
    connectedAndroidTest {
        ignoreFailures = true
    }
}